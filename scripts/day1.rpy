label khdDay1:
    $ new_chapter(2, u"КХДшное лето. День 1")

    $ day_time()
    $ persistent.sprite_time = "day"                        #смена оформления на летнее дневное
    
    window hide
    show screen nextDay("День первый", "screen tvar") with Dissolve(0.5)
    play sound khdAmbList["crash"]                          #экран начала дня
    $ renpy.pause(2)
    hide screen nextDay with Dissolve(0.5)
    window show

    scene bg int_bus                                        #в автобусе
    show blinking
    play music music_list["no_tresspassing"] loop           #тревога музыка
    bgd "ля, че так жарко"
    "Богдан ощутил себя евреем в 39ом"
    bgd "ААААААА{w} я женщина... ААААААА"
    bgd "лан, шучу"
    th "так всм а где я"
    th "кожанные сидушки, {w}яркий свет, {w}все выглядит рисованно, как в БЛ"
    th "ааааа, ну все понятно"
    th "я на стачке шахтеров в кузбассе в 1961"
    th "АААААААА {w}я в 1961... {w}так телефон есть, наушники есть... {w}фууух, не заскучаю..."
    th "аааааа, а зарядка?"

    play sound khdAmbList["metal_noise"]                  #звук перебирания          
    $ renpy.pause(2.0)
    "в рюкзаке Богдан нашел все: {w}тетрадки, ручки, другую ерунду, {w}жесткий диск, {w}интел кор ай 7, 
    {w}электрогитару, {w}дизельный корабельный двигатель, {w}и файнали..."
    stop sound
    scene cg svar_apparat with Dissolve(0.5)                #сварочный аппарат
    "он нашел зарядку для телефона"

    scene bg int_bus with Dissolve(0.5)                     #снова в автобусе
    th "топчик, {w}ну все, можно идти трудиться в шахту кузбасса на благо партии"
    play ambience ambience_camp_entrance_day loop           #звуки перед воротами
    scene bg ext_camp_entrance_day with Dissolve(0.5)       #ворота
    "Богдан вышел из автобуса и понял, что не не на кузбассе 1961го"
    th "co-be-hok{w} что бы это могло значить?{w} хмммм, а может тут на русском написано?"
    th "совенок... Я ЧЕ В БЕСКОНЕЧНОЕ ЛЕТО ПОПАЛ?????"
    play music khdMusicList["i_w_t_p_gachi"]                #музыка Ульяны гачи ремикс
    show bg ext_camp_entrance_day:
        linear 0.2 ypos 0.9
        linear 0.2 ypos 1.0                                 #прыжки
        repeat

    th "Урааа{w} я в бесконечном лете"
    "От радости Богдан запрыгал, забыв, что в его рюкзаке лежат жесткий диск, интел кор, электруха и двигатель"
    show bg ext_camp_entrance_day                           #ворота
    th "так стоп, {w}а Икарус то здесь еще?"
    scene bg ext_bus with Dissolve(0.5)                     #автобус
    th "Икарусик, мой Икарус"
    show blink
    $ renpy.pause(1)
    play music khdMusicList["semyaiz"]                      #семяизвержение Стрыкало
    show black                                              #закрытые глаза
    "Богдан всем телом прижался к икарусу"
    "Нежность и тепло разлились по его телу,{w} он готов был расствориться в Икарусе, остаться навсегда с ним, 
    слиться с ним воединое"
    "Его прекрасные формы так и ласкали кожу Богдана, {w}а приятная вибрация мотора возбуждала его"
    "а его окна...."
    sl "эммм...{w} извиняюсь?"
    th "да блять, кто там пришел"
    bgd "не мешай! {w}не видишь? я занят!"
    sl "И чем же ты занят, интересно мне знать?"
    bgd "Я питаюсь энергией денег, {w}ой, всмысле Икаруса"
    sl "ты тупой что-ли?"
    bgd "ЧЕ СКАЗАЛ?"

    scene bg ext_camp_entrance_day                          #вход в совенок
    show sl angry pioneer at center                         #Славя злая
    with Dissolve(0.5)
    play music music_list["pile"]                           #музыка наказание

    "Когда Богдан понял, что это была Славя, он проглотил язык, {w}буквально {w}и начал задыхаться, но не подал виду, так 
    как знал, что Славя точно обеспокоится и категорически этого не хотел"
    sl "Ну и че застыл? {w}женщину в первый раз увидел?"
    bgd "Да."
    "это была чистая правда"
    sl "а как так?"
    bgd "аче не норм для 12 лет?"
    sl "нет, только для 11 норм"
    bgd "лол, я в шкафу прячусь"

    stop music
    show sl smile pioneer at center                         #Славя улыбается
    sl "хахаха {w}а ты смешной"
    show sl normal pioneer at center                        #Славя нормальная
    sl "блин, я не представилась..."
    bgd "стой, можешь не представляться, я сам имя угадаю"
    show sl surprise pioneer at center                      #Славя удивлена
    sl "чеее, всмысле, а как?"
    bgd "ща увидишь"
    play music khdMusicList["chunga"]                       #смешная гачи чунга чанга
    "Богдан начал жоско танцевать чунга-чангу"
    show sl laugh pioneer at center                         #Славя смеется
    "Славю это сильно рассмешило, {w}она уже каталась по земле в конвульсиях"
    "к сожалению, Богдан вовремя остановился"
    stop music
    sl "нннну и..... ахахахахахаахахах..... {w}так как меня зовут"
    bgd "чутье подсказывает мне, ты Славяной будешь"
    show sl surprise pioneer at center                      #Славя удивлена
    sl "чтоооооо? {w}как ты угадал?"
    bgd "хз, на предиктычах"
    sl "ок пон"
    show sl normal pioneer at center                        #Славя нормальная
    sl "ты это, {w}тебе к вожатой ща надо"
    bgd "к Олечке?"
    sl "какой Олечке?"
    bgd "ну вожатой"
    sl "нееее, вожатую не так зовут, {w}блин у нее имя такое... {w} странное... {w}что-то на До..."
    th "опа, а это еще че за пиво и приколы? Какая еще До..."
    sl "ну короче это, тебе к дому треугольному"
    bgd "ага"
    hide sl normal pioneer with Dissolve(0.5)               #Славя ушла

    "Славя скрылась за воротами, а Богдан все еще обдумывал, что это за До"
    th "хммм... {w}Домашний пивзавод? {w}Ну во всяком случае мечтать не вредно"

    scene bg ext_clubs_day with Dissolve(0.5)               #Клубы
    play ambience ambience_camp_center_day loop
    "В раздумьях Богдан зашел в лагерь и подошел к зданию клубов"
    th "оооо, логово пидора...{w} ой простите кхдшников"
    th "то есть таких как я"
    th "ну надо навеное зайти к ним, поздороваться что-ли"

    play sound sfx_open_door_clubs_2                        #звук двери
    play ambience ambience_clubs_inside_day loop            #звук внутри клубов
    scene bg int_clubs_male_day                             #внутри клубов
    show mkr norm far at cright                             #Макар
    with Dissolve(0.5)
    th "это что Макар????"
    bgd "МАКАР ЭТО ТЫЫЫ?"
    show mkr sad far at cright                              #Макар грустный (испуганный)
    mkr "ААААА, кто здесь.... {w}а, ты новенький, ну ты почти угадал,{w} меня зовут Икар"
    show mkr norm far at cright                             #Макар
    th "почти как Икарус"
    "Богдан опять размечтался об Икарусе"
    bgd "ок, меня Богдан"
    bgd "аче ты тут делаешь то? Всмысле что за клуб, есть ли тут еще кто?"
    mkr "это игродельная студия, ну то есть мы тут игры под zx spectrum делаем и на кассеты записываем"
    th "чет припоминаю у бати были такие кассеты с пакманом, змейкой и тетрисом"
    bgd "а кто мы?"
    mkr "ну тут еще есть один чел, {w}его Тоха Доха зовут"
    mkr "но он ща в музклуб пошел, {w}кассеты у них пиздить, ато свои у нас закончились"
    mkr "тут такое дело, мы игру затеяли, там по сюжету чел просыпается в логове у зеленой гусеницы с 8 глазами, 
    {w}она называется элиподом,{w} а потом надо развозить флегму и нимбос, участвовать в элирекции и т д, и открывать новые
    миры, {w}а в конце игры происходит разлом четвертой стены и игрок в шоке"
    th "хахах, они че вангеров делают?)"
    mkr "игра, кстати, Вангеры называется"
    mkr "но как погляжу, ты слишком глуп внимать мне, что с тебя взять, несчастная личинка элипода, что зря жирел и породил
    тебя - урода"
    bgd "ага да, миры у вас там наверное называются фострал, глоркс, икспло, да?"
    show mkr smile far at cright                            #Макар улыбается
    mkr "а ты откуда знаешь? {w}ну хотя ладно, кстати, хочешь в наши игры поиграть?"
    
    play music khdMusicList["cool_mus"]                     #крутая музыка)
    bgd "а какие есть?"
    mkr "есть дота 2, недавно сделали, {w}на трубе играть умеешь?"
    bgd "нет, а что?"
    mkr "да, там просто управление такое, {w}тоже в рот надо брать"
    bgd "ну ниче, ща научусь)"
    show mkr smile far at goLeft                            #Макар уходит в кладовку
    play sound khdAmbList["metal_noise"]                    #звуки поиска 
    "Икар ушел в кладовку искать кассету с дотой 2"
    th "хммм, а тут так то норм челы, судя по всему обитают"
    stop sound 
    scene cg makar_dota with Dissolve(0.5)                  #Макар играет в доту
    mkr "ну ка погоди... {w}о, катка нашлась, я на кунке играть буду, ща покажу"
    mkr "во, чекай, вот это ходить, {w}вот так стрелять, {w}во ульта"
    mkr "ну, все давай садись на киберспорт-табуретку..."
    hide cg makar_dota
    stop music
    window hide
    $ renpy.movie_cutscene(khdVidList["dota"])              #видео с дотой
    window show

    scene bg int_clubs_male_day                             #в клубах
    show mkr smile far at cright                            #Макар улыбается
    with Dissolve(0.5)
    mkr "ну, как тебе?)"
    "на самом деле судьба компа кибернетиков чуть не повторила судьбу компа Богдана"
    bgd "ну, игра хорошая, только сложная, реакцию надо иметь, {w}короче мне не понравилось"
    show mkr sad far at cright                              #Макар грустный
    mkr "блиин, вот никто в нее играть не может"
    th "ну ниче, Икар, погоди, лет через 30 это станет мЕйНсТрИмОм"
    play sound sfx_open_door_clubs                          #открытие двери (Тоха)
    show mkr norm far at cright                             #Макар нормальный
    mkr "о, Тоха пришел"

    "в комнату зашел кудрявый чел, чем-то похожий на Электроника, но в отличии от него этот чел был темноволосым"
    show ant norm at cleft                                  #Антон нормальный
    with Dissolve(0.5)
    play music khdMusicList["pofig"]                        #музыка пофиг
    mkr "ну че, достал кассеты"
    ant "да, вот только это, когда в клубе был, Алиска спалила, вырвала кишки, обмотала мне вокруг головы, разорвала печень
    стулом, и этим же стулом по башке настучала"
    show mkr sad far at cright                              #Макар грустный
    mkr "это тебе такой ценой достались кассеты?"
    ant "а, не, это я ее за жопу лапнул, {w}думал незаметно будет, {w}кассеты то мне так просто дали, как члену клуба,
    не думал, что все так просто будет"
    show mkr smile far at cright                            #Макар веселый
    th "кибернетик, и еще в музклубе...{w} тут явно что-то не так"
    mkr "ну ты это, нормально все?"
    ant "побаливает чуть-чуть, но ниче, как-нибудь без кишков с печенью побегаю"
    show mkr smile at center                                #Макар рядом с Тохой
    mkr "у кошечки боли, у собачки боли, а у Тохи не боли"
    show ant smile at cleft                                 #Антон радостный
    ant "спасибо, уже прошло... {w}о, а это кто?"
    "Тоха показал на Богдана"
    show mkr norm at right                                  #Макар нормальный
    bgd "прив, я Богдан, токмачто приехал"
    ant "а, ну привет, я Тоха"
    ant "ты в наши игры играл уже?"
    bgd "да, в дотку скатал, но это, мне не понравилось"
    show ant sad at cleft                                   #Антон грустный
    ant "блин, ну это печально... {w}кстати, не хочешь помочь нам с новой игрой? {w}с вангерами {w}тебе ведь Икар про нее
    рассказал?"
    show ant norm at cleft                                  #Антон нормальный
    bgd "дааааа, рассказал... но это, а какие требования?"
    ant "В компанию гребцы и галеры трабуется разработчик со знанием C, C++, Java, C#, python, R, git, docker, javascript,
    react, angular, postgreessql, redis, mysql, mssql, mongo db, swss3enginex, apache, linux, sisd..."
    mkr "короче, не слушай его, ты как вообще, сам то хочешь?"
    bgd "хмммм, я пожалуй до обеда подумаю"
    mkr "ну ладно, пока тогда"
    ant "пока"
    bgd "давайте, удачи"

    play sound sfx_open_door_clubs_2                        #Богдан выходит из клуба
    scene bg ext_clubs_day with Dissolve(0.5)               #Клубы
    play ambience ambience_camp_center_day loop             #звуки природы)
    play music music_list["lightness"]                      #музыка хорошее настроение
    th "так, теперь мне надо к вожатой{w} за формой всякой и прочей херней"
    scene bg ext_houses_day with Dissolve(0.5)              #Дорога с домиками
    "Богдан прогулочным шагом шел по дороге между домиками"
    "Все таки не в каждый зимний день в пром. городе, где воздух наполовину состоит из углекослого газа,
    а на другую половину из паров серной кислоты, удается попасть в летний день, в место, где воздух чище жопы младенца"
    "Особенно, учитывая, что Богдан - астматик, такой воздух в прямом смысле его оздоравливает"
    play sound sfx_inhale                                   #Звук вдоха
    th "ебааать... {w}какой же тут охуенный воздух...."
    play sound sfx_inhale                                   #Звук вдоха
    play sound sfx_inhale                                   #Звук вдоха
    play sound sfx_inhale                                   #Звук вдоха
    th "надо им тут надышаться на всю жизнь вперед"
    th "а под конец смены нужно взять пакетов и набрать воздух с собой в пакеты, {w}а какие пакеты, я ж в ссср, здесь
    все с авоськами гоняли"

    play music music_list["revenga"]                        #Энергичная музыка
    "Поток филосовских мыслей Богдана прервал удар по затылку"
    with vpunch                                             #тряска вверх-вниз
    "он тут же понял, что это Алиса или Ульяна и запаркурил со скамейки на крышу чьего-то домика"
    scene cg dv_us_from_roof with Dissolve(0.5)             #Ульяна и Алиса, вид с крыши
    "Это оказались две рыжие сразу"
    th "ничесе у них удивленные лица"
    dv "скок паркуришь? {w}ток по чесноку"
    bgd "с 12 лет"
    dv "норм для 12 лет"
    stop music                                              #музыка выкл

    "поняв, что рыжие ему не угрожают, Богдан спустился с крыши, сделав сальто назад"
    play sound sfx_body_bump                                #звук падения
    scene bg ext_houses_day                                 #дорога с домиками
    show dv grin pioneer2 at cright                         #Алиса с ухмылкой
    show us grin pioneer at cleft                           #Ульяна с ухмылкой
    with Dissolve(0.5)
    dv "блин, аты реально нормис, как тебя зовут то?"
    bgd "Райан Гослинг"
    dv "кто?"
    bgd "лан, шучу, Богдан"
    dv "а меня зову..."
    bgd "...Алиса"
    show dv surprise pioneer2 at cright                     #Алиса удивлена
    show us surp2 pioneer at cleft                          #Ульяна удивлена
    dv "чее, откуда ты знаешь?"
    us "а мое имя угадаешь?"
    bgd "Ульяна"
    dv "чеееееее"
    us "чеееееее"
    bgd "ну вот так вот, сам хз как"
    bgd "мне к вожатой надо, я пойду"
    dv "ок."
    us "счастливо, Богдан!"
    hide dv surprise pioneer2
    hide us surp2 pioneer                                   #Алиса и Ульяна прячутся
    with Dissolve(0.5)
    $ renpy.pause(1)

    scene bg ext_square_day                                 #площадь
    show un sad pioneer far at left                         #серьезная Лена                         
    with Dissolve(0.5)
    th "площадь, на которой я уже был кучу раз, играя в бл"
    th "И Лена, все так же скучающе читающая"
    th "хмм... что же она читает?"
    play music music_list["raindrops"]                      #норм музыка
    show un sad pioneer close at center                     #серьезная Лена близко
    "Лена, занятая книгой, даже не заметила, как к ней подсел Богдан и начал пялиться в упор"
    bgd "что читаешь?"
    show un scared pioneer close at center                  #напуганная Лена близко
    $ renpy.pause(1)
    show un shy pioneer close at center                     #смущенная Лена близко
    un "ооойй... я напугалась"
    un "ммм... ну.... {w}это книга из библиотеки..."
    bgd "а как называется?"
    un "ну... вот в общем"
    "Лена показала Богдану свою книгу"
    scene cg malyshok_mares with Dissolve(0.5)              #ужасы малышка
    th "ого, а она - почитатель классики!"
    scene bg ext_square_day 
    show un shy pioneer close at center                     #снова Лена на площади
    with Dissolve(0.5)
    bgd "ого, я тоже читал эту книгу, {w}как тебе кстати момент, когда ночью какая-то тварь начала по стенам скрести?"
    show un smile2 pioneer close at center                  #Лена улыбается
    un "как ты ее уже прочитать успел? Она ведь недавно вышла"
    th "знала бы ты сколько раз я ее прочитал в своем времени"
    bgd "так это, у меня отец в Москву уезжал, мне ее еще месяц назад привез"
    show un surprise pioneer close at center                #Лена удивлена
    un "здорово, я так же хочу..."
    bgd "ну не расстраивайся, у меня с собой новые книжки есть, можешь взять"
    "Богдан действительно носил с собой на уроки всякую научную фантастику, ибо на уроках делать нефиг"
    show un smile3 pioneer close at center                  #Лена улыбается
    un "правда, правда?"
    bgd "да."
    un "было бы славно... {w}ой, что-то я тут тебя задерживаю, тебе наверное к вожатой надо, да?"
    bgd "ну да, не помешало бы, до встречи"
    un "удачи!"

    stop music                                              #музыка выкл
    scene bg ext_houses_day with Dissolve(0.5)              #дорога с домиками
    th "что бы ей дать такого, что я читал уже?"
    th "хммм... {w}есть роман Кадетство, {w}думаю ей понравится..."

    scene bg ext_house_of_mt_day with Dissolve(0.5)         #дом Ольдмитриевны (Доры)
    "в размышлениях Богдан подошел к домику вожатой"
    th "а вот и моя берлога на ближайшие 7 дней"
    th "ща мы и узнаем, что это за вожатая на До..."
    play sound sfx_medpunkt_door_open                       #звук двери
    play ambience ambience_int_cabin_day loop               #эмбиенс в домике
    scene bg int_house_of_mt_day                            #в доме вожатой
    show dor norm far                                       #Дора
    with Dissolve(0.5)

    play music khdMusicList["i_w_t_p_gachi"]                #Веселая музыка
    th "ЭТО ЧТО ДОРА?????"
    th "урааа, урааа вожатая Дора, ура, ура"
    dor "все хорошо? {w}ато у тебя лицо, как будто негра увидел"
    bgd "хочу стать твоим унитазом"
    show dor smile far                                      #Дора улыбается
    dor "ок, тогда тебе придется встать в очередь за Икаром, Тохой и Папичем"
    th "ого, так тут еще и Папич есть"
    bgd "блиииин, ну ладно"
    show dor norm far                                       #Дора нормальная
    stop music                                              #Музыка выкл
    dor "так, отставить шутки, ты ведь Богдан, да?"
    bgd "именно"
    dor "мест в домиках не осталось, поэтому тебе придется спать со мной, {w}хотя кто знает, что для тебя лучше, 
    сталкер... {w}ой, то есть пионер"
    th "мммм... моя мечта осуществилась"
    dor "вот твоя одежда, одевайся, через 5 минут обед, я пойду сейчас"
    "Дора протянула Богдану уже известную ему форму из шорт, рубахи и треугольничка-галстука"
    th "люди новы, а предрассудки стары..."
    "этими словами Богдан частично описал, почему он кхд"
    hide dor norm far with Dissolve(0.5)                    #Дора уходит
    play sound sfx_close_door_1                             #Звук закрытия двери
    "пока Богдан думал, Дора ушла"
    play sound khdAmbList["metal_noise"]                    #звук металла
    "Богдан выложил из карманов все самое ценное и начал одеваться в здешние тряпки"
    stop sound
    scene cg in_mirror with Dissolve(0.5)                   #Богдан в зеркале
    th "ого, почему в зеркале Леонардо Ди Каприо? {w} А ой, это я, оказывается"
    th "хмм, а ниче так, я секс..."
    "насмотревшись на себя в зеркале, Богдан вышел из домика на обед"

    play ambience ambience_camp_center_day loop             #эмбиенс на улице
    play sound sfx_close_door_1                             #Звук закрытия двери
    scene bg ext_house_of_mt_day                            #Домик Доры снаружи
    with Dissolve(0.5)
    th "так ну и че где горн этот ебучий"
    play sound sfx_dinner_horn_processed                    #звук горна
    th "вспомнишь говно - вот и оно"
    scene bg ext_houses_day with Dissolve(0.5)              #дорога с домиками
    "Богдан медленно поплелся на обед, параллельно разглядывая пионеров, корча им рожи, и понимая, что после своего 
    завтрака, который, спешим напомнить, разорвал ему кишки, он вряд ли сможет съесть что-то без урона для организма"
    "хотя это так, если он будет есть ртом... {w}кто знает, какие секретные тактики скрывает организм Богдана"
    th "значит так, блять, если там не будет чифира я разношу всю столовую в пух и прах, разбиваю тарелки о головы поваров
    достаю из них кишки и рисую ими на стенах лозунги о независимости прибалтики"
    "Богдан размышлял о компенсации за 140 литров чифира, невыпитых у него дома, когда мимо его глаз пронеслись два
    безмолвно-лазурных хвоста"
    
    play music music_list["timid_girl"]                     #музыка Мику
    show mi normal pioneer at cright with Dissolve(0.5)     #появление Мику
    $ renpy.pause(1)
    show mi surprise pioneer at cright                      #Мику удивлена
    mi "стоп стоп стоп стоп, ты пиуаа пиуаа чин чин чин Богдан, который суме... пиапиаипиаипиипаоаоаоаоаоао а пото...
    фоиримоаымиывоав, а можешь еще раз так?"
    bgd "чего? и разве мы знакомы?"
    "судя по всему заочно она уже была знакома, а Богдан и подавно" 
    show mi shy pioneer at cright                           #Мику смутилась
    mi "ой, прости, прости, не представилась, я Мику, бла бла бла батя инженер, мать из Жепена, бла бла бла, никто 
    не верит"
    bgd "а я Богдан"
    show mi smile pioneer at cright                         #Мику улыбается
    mi "ой, Алиса уже мне все про тебя рассказала, как ты искуссно по домам прыгаешь, можешь показать?"
    bgd "нет, мы так на обед опаздаем"
    mi "блииин, ну ладно"
    show mi normal pioneer at cright                        #Мику нормальная
    mi "а ты кстати на чем-нибудь играть умеешь? просто тут бла бла бла музклуб бла бла бла не хочешь вступить?"
    bgd "ну, играть на гитаре чуть-чуть умею, впринципе Все Идет По Плану сыграть могу, а так насчет клуба хз"
    mi "а что такое все идет по плану?"
    bgd "а... блин... {w} ну года через 2-3 поймешь"
    show bg ext_dining_hall_away_day with Dissolve(0.5)     #столовка снаружи
    mi "ну ладно, подумай пж, нам не хватает членов {w}в клубе"
    show bg ext_dining_hall_near_day with Dissolve(0.5)     #столовая ближе
    "в диалоге с Мику Богдан и не заметил, как они подошли к столовой"

    show bg int_dining_hall_people_day with Dissolve(0.5)   #внутри столовой
    play ambience ambience_dining_hall_full loop            #эмбиенс в столовой
    mi "ой Богдан, бла бла бла, за едой, потом бла бла бла"
    show mi normal pioneer at goLeft                        #Мику убегает
    stop music                                              #Музыка останавливается
    "Мику убежала за едой, а Богдан опять ниче не понял среди ее потока мыслей"
    th "так, че я там хотел... {w} ах, точно чифир на обед"

    scene cg oguzok_kitch with Dissolve(0.5)                #огузок на кухне
    bgd "значит так, Огузок, мне чифир, да покрепче"
    ogu "сейчас будет!"
    th "не уж то у них чифир есть?"
    play sound khdAmbList["sranie"]                         #звук срания(наливают чифир)
    scene cg chifir with Dissolve(0.5)                      #чифир
    ogu "твой чифир, пионер!"
    bgd "от души, Огузок!"

    scene bg int_dining_hall_people_day with Dissolve(0.5)  #внутри столовой
    "водя взглядом по столикам со зрением -2 и -8 на двух глазах соответственно, Богдан заметил лишь два свободных:
    с кибернетиками и с двумя рыжими и Мику"
    th "есть два стула..."

    menu lunchChoise:                                       #выбор с кем сесть на обеде
        "С кем сесть"
        "С девочками (я тупой куколд)":
            call khdGirlLunch
        "С мальчиками (я тру кхд)":
            call khdBoyLunch

    "продолжение"


#*************************************** ОБЕД С КИБЕРНЕТИКАМИ ****************************************

label khdBoyLunch:
    "(кхд партия гордится тобой!)"
    show mkr norm at cright
    show ant norm at cleft                                  #Тоха и Икар 
    with Dissolve(0.5)
    "кибернетики о чем-то очень бурно разговаривали"
    ant "я тебе говорю, как ты собрался mvc на ассемблере реализовывать?"
    mkr "легчайше: для начала на ассемблере напишем какой-нибудь ооп язык типо джавы, а потом на нем лабать будем..."
    bgd "извините, а у вас не занято?"
    ant "нет конечно, садись"
    show mkr norm close at cright
    show ant smile close at cleft                           #Тоха и Икар близко

    play music music_list["just_think"]                     #музыка подумать
    show blink
    $ renpy.pause(1)
    scene black
    "кибернетики напомнили Богдану о времени, когда он с другом пытался сделать игрорю в стиле фаллаута 1"
    th "дааа, хорошее было время"
    "Богдан полностью ушел в свои мысли, и разговоры кибернетиков лишь изредка доносились до его сознания"
    th "тогда я не боялся мечтать, думал, что все еще впереди, и будущее казалось светлым и радостным"
    th "теперь же я целыми днями просиживаю дома, я стал хикканом, который почти не выходит из дома, и лишь изредка я
    выхожу в школу, чтоб меня оттуда не выперли"
    th "где же я свернул не туда?"
    mkr "эй, Богдан, все хорошо"
    scene bg int_dining_hall_people_day
    show mkr sad close at cright
    show ant sad close at cleft
    show unblink
    $ renpy.pause(1)
    stop music

    bgd "дада, я просто задумался"
    show mkr norm close at cright
    show ant norm close at cleft                            #норм кибернетики
    mkr "ну ладно, ато ты просто сидишь, глаза закрыл и не двигаешься"
    bgd "ну, со мной бывает такое"
    "Повисло молчание, {w}Богдан начал пялиться в свою чашку с чифиром, раздумывая из каких выделительных жидкостей осла
    сделан этот чудесный напиток"
    ant "эй, а где твой обед?"
    bgd "вот он"
    "Богдан показал Тохе чифир"
    ant "а что ты есть будешь?"
    bgd "этот чифир, {w}он содержит все необходимые соли для работы моего организма"
    "(шутка) когда все люди на 70 процентов состоят из воды, Богдан на 90 процентов состоит из чифира"
    bgd "они всасываются прямо в кровь, а потом расходуются по мере необходимости"
    ant "круто, а можно мне попробовать?"
    mkr "и мне"
    bgd "ну попробуйте"
    play music music_list["that_s_our_madhouse"]            #энергичная музыка

    "ничего не подозревая, Богдан протянул чашку кибернетикам"
    show mkr sad close at cright
    show ant sad close at cleft                             #грустные кибернетики
    $ renpy.pause(0.5)
    "отпив по глотку, кибернетики сильно изменились в лицах: Тоха скривился, как будто ему Алиса снова кишки вырвала,
    а Икар аж черно-белым стал"
    show mkr sad close at goLeft
    show ant sad close at goLeft                            #кибернетики убегают
    th "хех, петушки неопытные, {w}вот постоянно надо молодняк учить чифир пить"
    stop music
    "посамоутверждавшись за счет кибернетиков, Богдан стал скучающе рассматривать людей"

    "вдалеке он увидел Дору"
    show dor norm far at right                              #норм Дора далеко
    with Dissolve(0.5)
    "Дора тоже заметила Богдана"
    "Он рукою помахал, она помахала в ответ, {w}он пошел к ней навстречу, {w}это было так глу... {w}ой, не та пластинка"
    hide dor norm far with Dissolve(0.5)
    "в это время вернулись кибенетики"
    show mkr sad close at cright
    show ant sad close at cleft                             #грустные кибернетики
    with Dissolve(0.5)
    ant "ну и напитки у тебя!"
    mkr "да уж"
    bgd "эх вы, ниче не понимаете"
    ant "сам-то эту дрянь выпьешь?"
    play sound khdAmbList["drinking"]                       #звук питья

    "Богдан с каменным лицом, залпом выпил чифир, наслаждаясь каждой ноткой его вкуса и смакуя каждую его капельку"
    stop sound
    "чифир вылился через дырявый желудок прямо во внутреннюю среду, сразу же всосался в кровь, {w}которая, по правде
    сказать, состояла на 99 процентов из чифира и на 1 процент из нефти, {w}и сразу же разбежался по организму, 
    усваиваться в разные органы"
    bgd "ммммм, вкуснотища"
    show ant smile close at cleft                           #Антон улыбается
    ant "а, это, кстати, {w}как насчет вступления в клуб?"

    menu kiberClubJoin:                                     #вступление в клуб кибернетики
        "Вступить в клуб"
        "Даааа!! (я тру кхд)":
            $ evCibClubJoin = True                          #вступление в клуб кибернетики
            $ rootTitan += 1                                #увеличение переменной рута титана кхд
            play music khdMusicList["opening_gachi"]        #музыка опенинг гачи
            show mkr smile close at cright                  #Икар улыбается
            ant "кайф"
            mkr "поздравляю со вступлением! {w}надо будет отпраздновать!"
            bgd "а как?"
            mkr "нууу... {w}у нас в кладовке видик есть, можно посмотреть что-нибудь, может у тебя есть кассеты?"
            th "ну да, каждый день с собой ношу"
            bgd "нет, у меня их нет"
            mkr "ну ничего, зато у нас есть... {w}ты смотрел Курьера?"
            th "ну так то да, и не раз"
            bgd "нет, а что за фильм?"
            mkr "сам хз, недавно купил, еще не смотрел"
            ant "ну насколько я знаю, там просто про чела какого-то"
            "весьма исчерпывающее описание"
            mkr "ну, давай тогда сегодня после ужина"
            bgd "да, давайте"
            hide mkr smile close
            hide ant smile close                            #кибернетики прячутся
            with Dissolve(0.5)
        "Неееет (я тупой даун)":
            show ant sad close at cleft                     #Антон грустный
            play music music_list["i_dont_blame_you"]       #грустная музыка
            ant "блиииин, почему?"
            mkr "даа, почему?"
            bgd "ну, а чем я могу вам помочь? {w}пива принести?"
            mkr "ну было бы славно"
            ant "хотя да, пользы от тебя наверное было бы мало, {w}но тем не менее рабочие руки нам бы не помешали"
            bgd "вот именно что РАБочие"
            ant "ну ладно, не хочешь как хочешь, удачи, че"
            mkr "ну да, ты заходи к нам если что, не стесняйся"
            hide mkr sad close
            hide ant sad close                              #кибернетики прячутся
            with Dissolve(0.5)

    stop music
    "Богдан встал изо стола, отнес бокал изпод чифира и уже было направился к выходу, как вдруг ему навстречу показалась
    чем-то обозленная Мику"
    play music music_list["you_lost_me"]                    #музыка злая
    show mi angry pioneer far with Dissolve(0.5)            #Мику злая далеко
    mi "Богдан!!!!!"
    show mi angry pioneer                                   #Мику злая ближе
    mi "ты совсем охуел... чин чан чин чаи пиуааа пиуааа взвзвзвзвзвзв фофовыроарваорарфокаро я ж тебе говорила пиирвсивра
    втмир чин чин чин, и почему ты с нами не сел?"
    bgd "че?"
    show mi rage pioneer                                    #Мику пиздец злая 
    mi "ты меня вообще слушаешь?"
    th "охуеть, блять"
    bgd "а ты не можешь помедленнее?"
    mi "а ой"
    show mi smile pioneer                                   #Мику улыбается
    play music music_list["timid_girl"]                     #Добрая музыка
    mi "прости, забываю, что ты затупо... ой {w}что я слишком быстро говорю"
    th "нихуя чсв"
    bgd "я прощаю тебя"
    mi "ой как милаааа.... кхм кхм"
    show mi shy pioneer                                     #Мику смущенная
    mi "ой, слушай, я зачем пришла то.... {w}как ты смотришь на вступление в клуб?"

    menu musicClubJoin:
        "Вступить в музклуб"
        "Даааааа":
            $ evMusClubJoin = True                          #вступление в муз клуб
            $ rootDv += 1                                   #рут Алисы
            show mi happy pioneer                           #Мику счастлива
            mi "здорово, здорово, ура ура, Богдан вступил в клуб, ура ура"
            th "ну как ребенок, ей слово скажи и радости полные штаны"
            mi "давай тогда пиуааа пиуааа пипипипипи рынь рынь рынь пиапиапиаиаиапфломиывлаарвга сейчас ипрваипвриа 
            шфгывипаови фываорим порепетируем"
            mi "все понял?"
            bgd "ниче не понял"
            show mi upset pioneer                           #Мику расстроена
            mi "ну вот, опять меня Богдан не понимает"
            bgd "ну не плачь только, лучше по человечески скажи"
            show mi smile pioneer                           #Мику улыбается
            mi "так {w}сейчас {w}мы {w}пойдем {w}репетировать"
            mi "ты с нами?"

            if evCibClubJoin:                               #если вступил в киберклуб
                th "ну вроде кибернетики меня после ужина звали..."

            th "хотя Дора может меня заставить обходной подписывать, {w}ну и похуй на него"
            bgd "да, давай"
            show mi happy pioneer                           #Мику счастлива
            mi "урааааа ураааа, здорово здорово, Богдан идет с нами репетировать"
            bgd "погоди, а с кем \"с нами\"?"
            show mi serious pioneer                         #Мику серьезная
            mi "ну в клубе еще Тоха состоит {w}и Алиса {w}но только они перед обедом, скажем так, поссорились, поэтому не
            знаю придет ли Тоха"
            th "хех, это она про то, как он ее за жопу лапнул, а она ему кишки вырвала?)"
            bgd "а если не секрет, что там произошло?"
            show mi laugh pioneer                           #Мику смеется
            mi "ахахахахахаха, там такая смешная ситуация вышла... {w}он ее за жопу лапнул, а она ему кишки вырвала, {w}
            кровищи было.... ахахахахаха"
            bgd "согл, смешно пиздец"
            th "хотя, если верить сюжету, у нее ж мать из Жепена, кто знает, может всратые фетиши передаются по наследству"

        "Неееееет":
            show mi upset pioneer                           #Мику печальная
            mi "блин, печально, а нам как раз людей не хватает, Алиска с Тохой перессорились, а у меня спид"
            bgd "что у тебя?"
            mi "а да так, {w}в больницу ходила, сказали, что спид, сама хз что это"
            th "блин ок"
            bgd "ну ты это сильно не расстраивайся, будет и на твоей улице праздник"
            show mi dontlike pioneer                        #Мику донтлайк
            mi "не люблю праздники, дискотека хочу"
            bgd "ну будет дискотека"
            show mi smile pioneer                           #Мику улыбается
            mi "правда правда, честно честно?"
            bgd "да, обещаю"
            mi "урааа урааа, у меня будет дискотека"

    mi "ну мне в общем это, {w}пора, там аппаратуру выставлять, подготавливать всякие гитары там, комбики и прочую
    бесполезную хуйню"
    if evMusClubJoin:
        mi "ты тоже приходи, поможешь что ли"
    
    mi "ну все, я побежала"
    show mi smile pioneer at goRight                        #Мику уходит
    bgd "лан, пока"
    "Богдан также направился к выходу из столовой"

    return

#********************************************* ОБЕД С ДЕВОЧКАМИ *****************************************************

label khdGirlLunch:
    show us normal pioneer far at left
    show dv normal pioneer2 far
    show mi normal pioneer far at right                     #девочки далеко
    with Dissolve(0.5)
    play music music_list["take_me_beautifully"]            #спокойная музыка
    "за столом сидели две рыжие и Мику"
    show mi smile pioneer far at right                      #Мику улыбается
    mi "ой, Богдан, иди сюда, хехей хехей"
    "Мику начала размахивать руками, будто она в толпе"
    th "вот интересно, это она искренне так ко мне относится, {w}или может усыпляет мою бдительность, чтоб потом вырубить
    и продать на органы?"
    th "хотя, в любом случае, {w}состояние моих органов оставляет желать лучшего"
    show us surp2 pioneer at left
    show dv normal pioneer2
    show mi smile pioneer at right                          #Девочки ближе

    us "ой Богдан, а что это у тебя такое в чашке?"
    menu chifirDrink:                                       #что сказать Ульяне про напиток
        ""
        "чифир":
            $ evs1Day["dvFall"] = True                      #Алиса упала (когда побежит за Ульяной)
            bgd "ну, это чифир, {w}судя по всему, оч крепкий, видишь как нефть"
            show us upset pioneer at left                   #Ульяна расстроена
            us "эхххх... жаль, {w}я думала это что-то вкусненькое"
            show dv grin pioneer2                           #Алиса ухмыляется
            dv "Ульяна, Ульяна, тебе бы только за чужой счет полакомиться"
            th "ага, кто бы говорил"
            show us angry pioneer at left                   #Ульяна злая
            us "а вот и не правда, {w}я просто за Богдана беспокоюсь, чтоб он всякое говно не пил"
            th "ТЫ ЧЕ, СУКА, ОХУЕЛА ТАК НАПИТОК БОГОВ НАЗЫВАТЬ"
            "Богдан еле сдержался от того, чтобы сказать это вслух"
            th "а хотя.... {w}ну ладно, прощу девочку в первый раз, неопытная сельдь все таки еще"
            dv "ой, какие мы благородные..."

            play music music_list["pile"]                   #жоская музыка
            play sound sfx_face_slap                        #Пощечина
            "вдруг Ульяна неожиданно дала Алисе пощечину"
            show mi scared pioneer at right                 #Мику напуганная
            show dv rage pioneer2                           #Алиса злая
            dv "ТЫ ЧЕ СОВСЕМ ОХУЕЛА???"
            play sound sfx_body_bump                        #падение
            show us angry at goLeft                         #Ульяна убегает
            hide dv rage pioneer2 with Dissolve(0.3)        #Алиса падает                   
            "Алиса тоже попыталась дать Ульяне пощечину, но та вовремя отодвинулась и начала убегать, {w}в резульятате 
            чего Алиса гулко упала на пол"
            show dv rage pioneer2 
            show dv rage pioneer2 at goLeft                 #Алиса убегает
            "но она быстро аклималась и побежала за своей обидчицей"



        "горячий шоколад)":
            $ rootDv += 1                                   #рут Алисы плюс
            th "о, ща прикольнемся"
            play music khdMusicList["i_w_t_p_gachi"]        #веселая гачи музыка
            bgd "горячий шоколад"
            show dv surprise pioneer2
            show mi surprise pioneer at right               #Мику и Алиса удивлены
            th "кажется клюнули"
        
    
    
