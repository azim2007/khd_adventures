label khdDay2:
    $ new_chapter(2, u"КХДшное лето. День 2")

    $ day_time()
    $ persistent.sprite_time = "day"                        #смена оформления на летнее дневное
    
    window hide
    show screen nextDay("День второй", "screen huev") with Dissolve(0.5)
    play sound khdAmbList["crash"]                          #экран начала дня
    $ renpy.pause(2)
    hide screen nextDay with Dissolve(0.5)
    window show

    play ambience ambience_int_cabin_day loop               #эмбиенс в домике
    scene bg int_house_of_mt_day                            #в домике Доры днем
    show unblink
    "солнышко во всю светило в окно домика, {w}Дора к времени пробуждения Богдана уже проснулась и 
    сидела на кровати, {w}Богдан вспоминал приснившийся ему сон"
    th "ммм, {w}помню снилось, как сижу на двощах и мечтаю о попадании в БЛ... {w}хех, в первый 
    раз я во снах мечтал о моей текущей реальности"
    show dor norm at right with Dissolve(0.5)               #Дора нормальная
    "в это время Дора решила напомнить о своем существовании"
    play music khdMusicList["feeling_good"]                 #хорошая музыка
    dor "йо, доброе утро"
    bgd "фываи фыафук ла и... {w}дааа, доброе"
    "после сна Богдан не мог нормально формулировать мысли, поэтому по началу он вывалил что-то на 
    языке своей души"
    th "блин, сколько времени то сейчас?"
    bgd "а сколько времени, и почему вы меня сами не разбудили?"
    dor "сейчас пол седьмого, подъем через полчаса только, {w}и еще, давай на ты"
    bgd "ок, го"
    th "новая ачивка в жизни: теперь я с Дорой на ты"

    bgd "ну, раз уж у меня есть полчаса, я пойду умоюсь, погуляю там"
    dor "ну давай, только смотри на линейку не опаздывай!"
    bgd "ага, хорошо, до встречи"
    "Богдан взял свои мыльно-рыльные и вышел на улицу"
    play sound sfx_close_door_1                             #звук закрытия двери
    scene bg ext_house_of_mt_day with Dissolve(0.5)         #дом Доры снаружи
    play ambience ambience_camp_center_day loop             #эмбиенс на улице
    "солнце уже было высоко над горизонтом, {w}все-таки лето, как никак"
    "по улицам уже бегали ранние пташки, {w}Богдану так и хотелось отвесить подзатыльник или 
    подножку кому-то из них"
    
    show bg ext_houses_day with Dissolve(0.5)               #дорога с домиками
    "но Богдан смог пересилить свое желание и направился по дороге к умывальникам"
    th "так, {w}надо бы еще помыться будет"
    show bg ext_washstand_day with Dissolve(0.5)            #умывальники
    "погруженный в свои размышления о том, чем бы еще ему заняться у умывальников, Богдан подошел 
    к бесконечным источникам ледяной воды"
    show bg ext_washstand2_day with Dissolve(0.5)           #умывальник
    "выбрав один из них, Богдан подошел к нему, открыл воду и стал выполнять свои табутаски на этом 
    месте"
    play sound sfx_water_sink_stream loop                   #журчание воды
    "прежде всего он почистил зубы тем самым советским порошком"
    th "фууу бля, {w}че он такой противный?"
    "несмотря на свою кхдшность, Богдан все же был зумером"
    "потом он умылся"
    show blinking                                           #моргание
    $ renpy.pause(2)
    "начитавшись в других модах про то, как Семен мылся здесь под краном, Богдан попытался 
    провернуть то же самое {w}(лол, а рил, как тут помыться?)"
    stop music fadeout 1.0
    "Богдану все же удалось направить струю на себя, и, предварительно раздевшись, он начал мыться"
    th "ай какая холодная"
    th "бррр... {w}я негр под палящим солнцем, {w}я негр под палящим солнцем, {w}даже самовнушение 
    не помогает"
    play sound sfx_close_water_sink                         #закрытие крана
    "кое-как помывшись, схватив 10 судорог жопы, Богдан стал одеваться, {w}он начал надевать рубашку 
    как вдруг его сзади кто-то окликнул"

    show bg ext_washstand_day with Dissolve(0.5)            #умывальники
    sl "о, Богдан, доброе утро"
    show sl normal sport with Dissolve(0.5)                 #Славя нормальная
    play music music_list["get_to_know_me_better"]          #дружеская музыка
    "этим человеком, любившим вставать пораньше, конечно же, оказалась Славя"
    bgd "ахайо.. {w}ой, доброе утро, Славя"
    "Богдан вспомнил, что за аниме здесь наверняка бьют без выяснения причин"
    sl "слушай, тут такое дело..."
    th "какая Славя сочная в этом костюме, {w}она приторная кшн, но все же сочная"
    sl "ну как ты на это смотришь?"
    bgd "а, что?"
    show sl angry sport                                     #Славя злая
    sl "ты вообще меня слушал?"
    bgd "прости, засмотрелся"
    sl "дааааа, ты мне еще у ворот странным показался"
    th "еще бы, {w}приезжает чел, обнимается с икарусом, а потом чунга-чангу танцует"
    show sl normal sport                                    #Славя нормальная
    sl "в общем, {w}ты с кем на субботник идешь?"

    if evs2Day["cleaning"] == "sl":
        bgd "ни с кем, {w}пока"
        show sl shy sport                                   #Славя смущена
        sl "как тогда насчет помочь мне в уборке территории?"
        bgd "ну, го"
        show sl smile sport                                 #Славя улыбается
        sl "здорово, ато у меня вообще никогда помощников не бывает"
        bgd "а что, никто дружить не хочет?"
        show sl normal sport                                #Славя нормальная
        sl "не знаю на самом деле, может и так"
        bgd "да не, я ж пошутил"
    else:
        if evs2Day["cleaning"] == "titan":
            bgd "с кибернетиками"
        else:
            bgd "с Женей"

        show sl sad sport                                   #Славя грустная
        sl "аааа, жаль, {w}мне просто помочь никто не может, я думала хоть ты свободен, ну да ладно"
        bgd "а что там у тебя, {w}план по выпуску ядерного оружия горит?"
        show sl laugh sport                                 #Славя смеется
        sl "неееее, {w}просто площадь подмести надо"
        bgd "мммм, ну прости, как-нибудь в следующий раз"
        show sl normal sport                                #Славя нормальная
    
    sl "пойдем на линейку, у нас 15 минут есть"
    bgd "ну мне сначала полотенце с мылом унести надо"
    sl "ну давай отнесем, а потом на линейку"
    bgd "ну пойдем"
    window hide
    show bg ext_houses_day with Dissolve(0.5)               #Дорога с домиками
    $ renpy.pause(1)
    show bg ext_house_of_mt_day with Dissolve(0.5)          #Домик Доры
    window show
    "Богдан зашел в домик, а Славя осталась снаружи"

    play sound sfx_open_door_1                              #открытие двери
    play ambience ambience_int_cabin_day loop               #эмбиенс в домике
    scene bg int_house_of_mt_day with Dissolve(0.5)
    "Богдан кинул свои принадлежности в свой шкаф, {w}Доры уже не было в домике, видимо она ушла собирать всех на 
    линейку"
    th "а может здесь остаться и не пойти никуда? {w}хотя меня там Славя ждет, видно идти придется"
    "надпись \"совесть - лучший контроллер!\" в советских автобусах явно была не про Богдана"
    play sound sfx_close_door_1                             #закрытие двери
    play ambience ambience_camp_center_day loop             #эмбиенс на улице
    show bg ext_house_of_mt_day                             #домик Доры снаружи
    show sl normal sport                                    #Славя нормальная
    with Dissolve(0.5)

    sl "ну все, теперь пойдем"
    bgd "Славь"
    sl "что?"
    bgd "а ты так и пойдешь на линейку?"
    show sl surprise sport                                  #Славя удивлена
    sl "ой, чуть не забыла, хоть ты мне напомнил"
    show sl shy sport                                       #Славя смущена
    sl "слушай, ты не против сходить со мной до домика, подождать меня, пока я не переоденусь?"
    "сам Богдан был не против, но его спермотоксикоз был хорошим аргументом отказаться от сего мероприятия, 
    дабы никто не пострадал"
    th "эххх, {w}какой соблазн будет подглянуть"
    bgd "ну го"
    show sl normal sport                                    #Славя нормальная

    window hide
    show bg ext_houses_day with Dissolve(0.5)               #дорога с домиками
    $ renpy.pause(2.0)
    show bg ext_house_of_sl_day with Dissolve(0.5)          #домик Слави
    window show
    sl "ты тут смотри, чтоб никто не подглядывал"
    bgd "хорошо"
    th "ну, смотреть за самим собой я смогу, пожалуй"
    hide sl normal sport with Dissolve(0.5)                 #Славя уходит
    play sound sfx_close_door_1                             #дверь закрывается
    "Славя скрылась за дверью своей обители, {w}Богдан решил присесть на скамейку, дабы отдохнуть"
    "день только начался, а он уже устал, {w}а ведь сегодня субботник"
    th "бля, автор, не напоминай"
    "ладно, ладно, молчу"
    "..."
    "Богдан сидел в тишине, наслаждаясь утренним ветерком, пока из-за двери не показалась Славя в пионерской форме"

    play sound sfx_open_door_1                              #звук открытия двери
    show sl normal pioneer with Dissolve(0.5)               #Славя нормальная
    sl "никто тут не подглядывал?"
    bgd "неа, я смотрел, все честно"
    sl "ну, хорошо, {w}тогда пойдем на линейку, она уже наверное началась"
    show bg ext_houses_day with Dissolve(0.5)               #дорога с домиками
    th "какой раз я уже за это утро иду по этой дороге?"
    show bg ext_square_day with Dissolve(0.5)               #площадь

    stop music fadeout 1.0
    "все уже были на линейке"
    show cg lineyka with Dissolve(0.5)                      #линейка
    "Дора начинала рассказывать про планы Совенковой плановой экономики на день, а 
    именно - про субботник, про то, как важно трудиться, бла бла бла, {w}и.... {w} ДИСКОТЕКА????"
    th "мммм, а дискотека - звучит по попсоебски, {w}НЕ ПОЙДУ ТУДА НАХУЙ"
    "мысли говорят сами за себя. {w}даже не ждите медляков с сопливыми описаниями обжиманий, стояков и смущений"

    scene bg ext_square_day with Dissolve(0.5)              #площадь

    if evs2Day["cleaning"] == "sl":                         #если субботник со славей
        call slMorningDay2khd
    elif evs2Day["cleaning"] == "mz":                       #если субботник с Женей
        call mzMorningDay2khd
    elif evs2Day["cleaning"] == "titan":                    #если субботник с кхд
        call titanMorningDay2khd
    else:                                                   #дебаг от бога
        "ERROR: cleaning имеет неправильное значение"

    play music music_list["two_glasses_of_melancholy"]      #спокойная музыка
    