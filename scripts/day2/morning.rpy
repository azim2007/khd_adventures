#***************************** ПРОДОЛЖЕНИЕ УТРА СО СЛАВЕЙ (ЕСЛИ СУББОТНИК СО СЛАВЕЙ) **********
label slMorningDay2khd:
    show sl normal pioneer with Dissolve(0.5)                   #Славя нормальная
    "так как на субботник Богдану приходилось идти со Славей, на линейку он тоже решил остаться с ней"
    sl "ну, мы вроде немного пропустили"
    th "ага, слушать про то, как нам сегодня впахивать придется, за что нам награда - дискотека, кою я ненавижу"
    bgd "ну, во всяком случае, мы знаем, что после завтрака субботник"
    play sound sfx_dinner_horn_processed                        #горн к еде
    "наконец, прозвучал горн, разрешающий пионерам пойти на местную заправку едой"
    dor "...вот и все, дорогие пионеры, можем идти на завтрак"
    
    play music music_list["smooth_machine"]                     #спокойная музыка
    "ну, и все пионеры дружной толпой пошли на завтрак, {w}Богдан шел вместе со Славей"
    show dor norm at right with Dissolve(0.5)                 #Дора нормальная
    "вдруг рядом с Богданом возникла Дора"
    dor "ну как, Богдан, выбрал с кем на субботник пойдешь?"
    bgd "да, вот, со Славей иду"
    dor "точно? ты не обманываешь?"
    bgd "нет, ты что"
    sl "да, Дора, он со мной"
    dor "ну тогда ладно"
    show bg ext_dining_hall_away_day with Dissolve(0.5)         #Столовая далеко

    hide dor norm with Dissolve(0.5)                            #Дора уходит
    "Дора узнала все, что ей надо было и удалилась"
    show sl surprise pioneer                                    #Славя удивлена
    "когда Дора отошла на достаточное растояние Славя решила поинтересоваться у Богдана"
    sl "Богдан, а почему ты с Дорой на ты?"
    bgd "ну, сегодня с утра она мне сказала, что с ней можно общаться на ты"
    sl "странно, мне она такого не говорила"
    show bg ext_dining_hall_near_day with Dissolve(0.5)         #Столовая далеко
    th "хех, я у нее на хорошем счету что ли?"
    show sl normal pioneer
    "наконец пионеры подошли к столовой"

    show bg int_dining_hall_people_day with Dissolve(0.5)       #в столовой людно
    play ambience ambience_dining_hall_full loop                #эмбиенс в столовой
    "Славя взяла себе манную кашу, {w}Огузок без слов понял, чего желает испить Богдан"
    "Славя с Богданом проследовали к свободному столу и уселись друг напротив друга"
    show sl surprise pioneer close                              #Славя близко удивлена
    sl "ууу, Богдан, {w}а что это у тебя в чашке"
    "Богдану уже порядком поднадоело объяснять свои гастрономические предпочтения, поэтому о чифире 
    он решил сообщить максимально кратко"
    bgd "чифир"
    sl "ух, ты, {w}и как?"
    bgd "как, как, {w}вкусно и питательно"
    sl "я пробовала его, мне не понравилось"
    bgd "ну, не всем нравится"
    th "кшн, чтоб его пить нужно быть элитой"
    
    show sl normal pioneer close                                #Славя нормальная
    play sound khdAmbList["drinking"]                           #звук питья
    "Богдан залпом выпил чифир и поставил чашку на стол"
    stop sound
    "остаток завтрака Богдан ждал, пока Славя доедает свою кашу"
    "..."
    sl "все, пойдем, я покушала"
    show sl normal pioneer                                      #Славя нормальная
    "Богдан со Славей вместе встали из-за стола и отнесли посуду в мойку"

    show bg ext_dining_hall_near_day with Dissolve(0.5)         #столовая рядом
    play ambience ambience_camp_center_day loop                 #эмбиенс в лагере
    show sl smile pioneer                                       #Славя улыбается
    stop music fadeout 1.0
    sl "ну все, Богдан, {w}теперь приступаем к социалистическому труду"
    th "хмм, она улыбается? {w}то есть ей рил приносит удовольствие работать за бесплатно?"
    
    show bg ext_dining_hall_away_day with Dissolve(0.5)         #столовая далеко
    if evMusClubJoin:
        show dv smile pioneer2 at right with Dissolve(0.5)      #Алиса улыбается
        "в это время из столовой показалась Алиса"
        dv "ну, что Богдан, завербовали тебя? {w}Ну ты трудись, трудись, труд облагораживает 
        человека!"
        show sl angry pioneer                                   #Славя злая
        sl "а ты, Алиса, почему не трудишься? {w}И вообще, Богдан, между прочим, добровольно 
        согласился мне помочь"
        th "ага, была б моя воля, я бы предпочел гитары двигать, а не площадь мести"
        show dv sad pioneer2 at right                           #Алиса грустная
        dv "ну, вообще-то, я сейчас и шла в клуб, чтоб убираться, а к вам подошла, чтобы сказать 
        Богдану, что сегодня после обеда опять репетиция"
        th "да какая нах репетиция? {w}че мы репетируем то?"
        dv "ты придешь?"
        bgd "да, приду"
        sl "ну все, а теперь иди уборку делай"
        dv "все, ухожу, ухожу"

        show sl normal pioneer                                  #Славя нормальная
        hide dv sad pioneer2 with Dissolve(0.5)                 #Алиса уходит
    
    sl "ну все,{w} пойдем за метлами и начнем убираться"
    bgd "ну пойдем"

    window hide
    show bg ext_square_day with Dissolve(0.5)                   #площадь
    $ renpy.pause(0.5)
    show bg ext_houses_day with Dissolve(0.5)                   #дорога с домиками
    $ renpy.pause(0.5)
    show bg ext_washstand_day with Dissolve(0.5)                #умывальники
    window show
    sl "на, это твоя метла"
    "Славя протянула Богдану непонятно откуда взявшуюся у умывальников метлу"
    bgd "эээм, Славь, а откуда здесь метлы?"
    sl "лол, я хз"
    window hide
    show bg ext_houses_day with Dissolve(0.5)                   #дорога с домиками
    $ renpy.pause(0.5)
    show bg ext_square_day with Dissolve(0.5)                   #площадь
    window show

    sl "ну в общем здесь метем. {w}Нанинай от центра площади и веди пыль к ее краям, {w}понятно"
    bgd "даааа"
    sl "тогда давай, {w}это твоя половина, это моя половина"
    "показав рукой, где Богдану нужно мести, Славя удалилась на свой участок"
    hide sl normal pioneer with Dissolve(0.5)                   #Славя прячется

    th "такс, ну приступим"
    play music music_list["goodbye_home_shores"]                #музыка для дел
    "Богдан взял метлу, подошел к Генде и начал мести, как ему показала Славя"
    
    play sound sfx_broom_sweep                                  #звук подметания
    window hide
    $ renpy.pause(2.0)
    window show

    th "ну, не сложно, {w}думаю справлюсь"
    "поспешно сделав вывод о легкости этого дела, Богдан стал мести площадь от центра к ее краям, 
    периодически чихая из-за пыли, {w}в голову ему лезли разные мысли, философские и не очень"
    th "хммм, {w}иронично, {w}слово Охрана наоборот читается как Анархо, {w}это такой тонкий намек 
    на то, что охрана делает совсем не то, что должна?"
    stop sound
    "{i}звук подметания наверное мешает, я его выключил{/i}"
    "Богдан посмотрел на Славю, {w}она была полностью сконцентрирована на своем деле"
    th "ну, ладно, придется и дальше говорить с самим собой"
    "..."
    "постепенно ебучая жара начала о себе напоминать: из-за физической работы под палящим солнцем 
    у Богдана по спине потекли ручейки"
    "прогноз Богдана о том, что работа легкая оказался ложным: из-за постоянных сокращательных 
    движений руками, они у него начали ныть"
    th "бляяя, у нас перекур будет, не?"

    play music khdMusicList["pofig"]                            #музыка пофиг
    "тут Славя как будто прочитала мысли Богдана и подошла к нему для объявления перекура"
    
    show sl normal pioneer with Dissolve(0.5)                   #Славя нормальная
    sl "объявляю перерыв, {w}можно сходить попить"
    bgd "урааа"
    th "ну все, ща помоюсь"
    hide sl normal pioneer with Dissolve(0.5)                   #Славя прячется
    "и Богдан тут же побежал к умывальникам, чтобы помыться"

    play sound sfx_run_forest                                   #звук бега
    window hide
    show bg ext_houses_day at khdRun with Dissolve(0.5)         #дорога с домиками
    $ renpy.pause(1.0)
    show bg ext_washstand_day with Dissolve(0.5)                #Умывальники
    stop sound fadeout 0.5

    th "ну все, ща помоемся"
    scene bg ext_washstand2_day with Dissolve(0.5)               #умывальник близко
    "Богдан быстро начал стягивать с себя одежду, дабы помыться под холодненькой водой"
    play sound sfx_blanket_off_stand                            #звук всполох
    "Богдан бросил одежду на пол и открыл кран"
    play sound sfx_water_sink_stream                            #струя воды
    th "мммм, {w}как приятно"
    "после долгой работы под солнцем Богдану уже было все равно чем мыться, {w}хоть горячим свежим 
    супом из столовой, хоть ледянющей водой из-под крана"
    play sound sfx_close_water_sink                             #закрытие крана
    "Богдан неторопясь оделся, {w}прилипающая к мокрой коже одежда не давала сделать это быстрее"
    th "ну ниче, {w}по дороге высохну"

    show bg ext_washstand_day with Dissolve(0.5)                #умывальники далеко
    "обратно Богдан пошел неспеша"
    stop music fadeout 1.0
    show bg ext_houses_day with Dissolve(0.5)                   #дорога с домиками
    $ renpy.pause(2.0)
    show bg ext_square_day with Dissolve(0.5)                   #площадь
    show sl normal pioneer far with Dissolve(0.5)               #Славя нормальная
    "Славя уже была на месте и подметала свою территорию"
    sl "о, вернулся, {w}а ну давай выше нос, {w}всего полчаса осталось и обед"

    hide sl normal pioneer with Dissolve(0.5)                   #Славя прячется
    th "что ж, {w}ну ладно"
    "остаток субботника Богдан провел в своих мыслях, машинально повторяя одни и те же действия 
    подметания"
    window hide
    play sound sfx_dinner_horn_processed                        #звук горна
    $ renpy.pause(2.0)
    window show
    "от мыслей его оторвал звук горна, означавший время обеда"
    show sl normal pioneer with Dissolve(0.5)                   #Славя нормальная
    "к Богдану подошла Славя"
    show sl shy pioneer                                         #Славя смущена
    sl "ты не мог бы мне помочь отнести метлы обратно?"
    bgd "кшн помогу, о чем базар то?"
    show sl smile pioneer                                       #Славя улыбается
    show bg ext_houses_day with Dissolve(0.5)                   #дорога с домиками
    "ну и пошел Богдан вместе со Славей относить метлы обратно к умывальникам"
    show bg ext_washstand_day with Dissolve(0.5)                #умывальники
    sl "спасибо, что помог"
    bgd "пожалуйста, {w}а теперь давай на обед пойдем"
    show sl normal pioneer                                      #Славя нормальная
    sl "давай"
    show bg ext_houses_day with Dissolve(0.5)                   #дорога с домиками
    "ну и пошел Богдан вместе со Славей на обед"

    window hide
    show bg ext_square_day with Dissolve(0.5)                   #площадь
    $ renpy.pause(1.0)
    show bg ext_dining_hall_away_day with Dissolve(0.5)         #столовая
    $ renpy.pause(1.0)
    show bg ext_dining_hall_near_day with Dissolve(0.5)         #столовая ближе
    $ renpy.pause(1.0)
    window show
    hide sl normal pioneer with Dissolve(0.5)                   #Славя уходит
    "Славя шла быстрее Богдана и уже зашла в столовую"
    "Богдан зашел следом за ней"
    return

#***************************** ПРОДОЛЖЕНИЕ УТРА С ЖЕНЕЙ (ЕСЛИ СУББОТНИК С ЖЕНЕЙ) **********
label mzMorningDay2khd:
    show mz normal glasses pioneer with Dissolve(0.5)           #Женя нормальная
    "Богдан помнил, что при вступлении в библиотеку, он пообещал Жене помочь в уборке и потому встал 
    рядом с ней"
    bgd "доброго"
    show mz bukal glasses pioneer                               #Женя с презрением
    mz "аааа...{w} да, доброе утро"
    show mz normal glasses pioneer                              #Женя нормальная
    "Женя была очень заспанной, будто всю ночь таскала уголь в шахтах"
    "наконец, Дора закончила рассказывать планы на день, и как раз вовремя прозвучал горн на завтрак"
    
    play sound sfx_dinner_horn_processed                        #горн
    $ renpy.pause(1.0)
    "все пионеры толпой пошли на завтрак"
    play music music_list["went_fishing_caught_a_girl"]         #веселая музыка
    "Богдан решил поприкалываться над Женей"
    bgd "йоу, че такая кислая?"
    "вопреки ожиданиям Богдана Женя ответила на вопрос спокойно"
    mz "спалось плохо"
    th "ну и ладно"
    show bg ext_dining_hall_away_day with Dissolve(0.5)         #столовая
    "издалека Богдан заметил, что у входа столпилась приличная толпа личинусов"
    th "ну ниче, растолкаем"
    show bg ext_dining_hall_near_day with Dissolve(0.5)         #столовая ближе
    bgd "А НУ РАССТУПИЛИСЬ ВСЕ, {w}пожалуйста"
    show mz smile glasses pioneer                               #Женя улыбается
    "благодаря волшебному слову все послушались Богдана"

    show bg int_dining_hall_people_day with Dissolve(0.5)       #в столовой
    play ambience ambience_dining_hall_full loop                #в столовой людно
    "короткая речь Богдана впечатлила Женю и окончательно смахнула с нее остатки сонливости"
    mz "нихуя ты их разогнал"
    bgd "ну, я и сам своего рода сигма гигачад"
    show mz bukal glasses pioneer                               #Женя с презрением
    mz "есть пошли, {w}сигма гигачад"
    show mz normal glasses pioneer                              #Женя нормальная
    "Женя взяла у огузка порцию манной каши, {w}что выдавать Богдану, Огузок уже знал"
    "после этого, пионеры уселись друг на против друга и приступили к трапезе"
    show mz normal glasses pioneer close                        #Женя близко
    "во премя еды Богдан заметил, что Женя снова начала клевать носом"
    th "дааа, с засыпающей Женей мне, видимо, придется делать уборку в соляново..."
    "и, недолго думая, Богдан вылил пол стакана чифира в компот Жене"
    "когда Женя взяла компот, чтобы попить ее ждал приятный сюрприз"
    
    window hide
    play sound khdAmbList["drinking"]                           #звук питья
    $ renpy.pause(1)
    show mz angry glasses pioneer close                         #Женя злая
    $ renpy.pause(0.5)
    show mz normal glasses pioneer close                        #Женя нормальная
    stop sound
    window show

    "зрачки ее резко уменьшились, сама она выпрямилась и уставилась прямо вперед"
    mz "я полна сил и энергии до конца дня"
    th "хех, сработало"
    show mz normal glasses pioneer                              #Женя нормальная
    stop music fadeout 1.0
    "вместе отнеся посуду в мойку, Богдан с Женей направились к выходу"
    
    show bg ext_dining_hall_near_day with Dissolve(0.5)         #столовая близко
    play ambience ambience_camp_center_day                      #эмбиенс в лагере
    mz "бля, Богдан, че за хуйня, {w}я помню, что буквально засыпала на завтраке, а теперь у меня 
    сна ни в одном глазу"
    bgd "хз, наверное в компот случайно кофе попало, ну или энергетик"
    mz "мм, ну ладно"
    show bg ext_dining_hall_away_day with Dissolve(0.5)         #столовая
    "Женя с Богданом пошли в библиотеку, дабы начать уборку"

    window hide
    show bg ext_square_day with Dissolve(0.5)                   #площадь
    $ renpy.pause(1.0)
    show bg ext_houses_day with Dissolve(0.5)                   #дорога с домиками
    $ renpy.pause(1.0)
    show bg ext_library_day with Dissolve(0.5)                  #библиотека
    window show

    "наконец они подошли к кладовой знаний всея совенок"
    play sound sfx_open_door_1                                  #открытие двери
    play ambience ambience_library_day loop                     #эмбиенс в библиотеке
    show bg int_library_day with Dissolve(0.5)                  #в библиотеке

    mz "ну-с, {w}давай начнем, {w}для началась давай пыль отовсюду протрем, {w}вот тряпка тебе"
    "Женя протянула Богдану кусок простыни"
    mz "ты иди туда протирай, а я туда пойду"
    "Женя рукой показала Богдану, где ему протирать, и сама удалилась на свой участок"
    
    hide mz normal pioneer with Dissolve(0.5)                   #Женя уходит
    th "ну, давайте приступим"
    "Богдан протирал книги, параллельно разглядывая их названия"
    th "так-с... {w}Толстой, {w}Горький, {w}Грибоедов, {w}оооо, {w}Ницше...."
    "впринципе дело было не сложное, {w}учитывая, что Богдану было интересно, какие книги находятся в 
    этой библиотеке"
    th "... {w}Стругацкие, {w}Стругацкие, {w}Стругацкие, да тут у них целая полка Стругацким 
    посвящена!"
    th "Лем, {w}Кант, {w}эммм... Оксимирон?"
    show cg gorgorod with Dissolve(0.5)                         #обложка горгород
    th "а, не, {w}это его альбом на виниле..."
    scene bg int_library_day with Dissolve(0.5)                 #в библиотеке
    "Богдана вообще ничего не смущало"
    "..."
    "время пролетало незаметно, как вдруг в библиотеку кто-то вошел"

    play sound sfx_open_door_1                                  #дверь открывается
    show mi normal pioneer with Dissolve(0.5)                   #Мику нормальная
    play music music_list["so_good_to_be_careless"]             #музыка Мику
    mi "ой, ничего себе, Богдан что, и прада тут, в общем я это, тут такое дело, вечером мы 
    выступать будем на сцене, нам там надо усилители из под сцены вытащить, а я теамноты боюсь, 
    Богдан, поможешь, пожалуйста"
    show mi shy pioneer at cright                               #Мику смцщается
    "в это же время из-за шкафов показалась Женя"
    show mz normal glasses pioneer at cleft with Dissolve(0.5)  #Женя нормальная
    mz "привет, Мику, ты что-то хотела?"
    mi "да, мне вот помощь Богдана нужна, усилитель вытащить"

    menu choise_help_mi:
        "помочь Мику":
            $ rootMz += 1                                       #Рут Жени +
            th "ну, одна Мику там явно не справится"
            bgd "ладно, давай помогу, че"
            show mi smile pioneer at cright                     #Мику улыбается
            mz "но только я с вами пойду"
            th "с чего бы это?"
            show bg ext_library_day with Dissolve(0.5)          #рядом с библиотекой
            play sound sfx_close_door_1                         #дверь закрывается
            play ambience ambience_camp_center_day loop         #эмбиенс на улице

            "Богдан с девочками дружной процессией двинулся по направлению к сцене"
            show bg ext_square_day with Dissolve(0.5)           #площадь
            th "здарова, Генда"
            show bg ext_stage_normal_day with Dissolve(0.5)     #сцена
            mi "вот, тут они под сценой, {w}сейчас я открою..."
            play sound sfx_open_door_1                          #открытие двери
            "Мику открыла ключом маленькую дверцу, ведущую в закрома под сценой"
            mi "вот, там вот усилки..."
            th "а там уютненько"
            show mi shy pioneer at cright                       #Мику смущается
            mi "Богдан, Женя, не могли бы вы потом поставить усилители на сцену и закрыть дверь, 
            ато мне надо спешить, занавески в клубе менять, одна Алиса не справится, а я и так тут 
            задержалась, ибо темноты боюсь"
            th "спешить, спешить, {w}всем нам куда-то спешить надо..."
            "пока Богдан ворчал про себя как старый дед, Женя уже ответила Мику не в самом хорошем 
            тоне"
            show mz bukal glasses pioneer at cleft              #Женя с презрением
            mz "да иди уже, {w}деловая колбаса"
            
            window hide
            show mi dontlike pioneer at cright                  #Мику донтлайк
            $ renpy.pause(0.5)
            show mi smile pioneer at cright                     #Мику улыбается
            window show
            "поначалу Мику скорчила недовольную репликой Жени рожицу, но потом все же улыбнулась и 
            выдавила из себя благодарность"
            mi "большое спасибо, ребят, вы настоящие товарищи"
            hide mi smile pioneer with Dissolve(0.5)            #Мику уходит
            stop music fadeout 1.0
            "и упорхнула, напевая себе под нос какую-то песенку"
            th "хех, {w}товарищи... {w}какое архаичное на самом деле слово..."

            show mz angry glasses pioneer at center             #Женя злая
            "из раздумий Богдана вырвала обозленная Женя"
            mz "вот эгоистка, самой ей, видите ли, страшно усилки таскать, {w}зачем ты вообще на это 
            согласился?"
            "Богдан насупился и выдал базу"
            bgd "я думал, она мне даст конфетку"
            show mz laugh glasses pioneer                       #Женя смеется
            "такой неожиданный ответ рассмешил Женю"
            mz "какой же ты наивный, {w}ладно, пойдем вытаскивать"
            show mz normal glasses pioneer                      #Женя нормальная
            bgd "а ты тоже что ли таскать будешь?"
            mz "ну да, а че я так просто что ли с тобой пошла?"
            bgd "хз, я думал, тебе скучно будет в библиотеке одной"
            show mz shy glasses pioneer                         #Женя смущается
            mz "ну, {w}да"
            th "так и знал, так и знал"
            show mz normal glasses pioneer                      #Женя нормальная
            "Богдан положил ключ, который ему дала Мику, в карман и направился в неизвестность, что 
            находилась под сценой"
            "и....."
            
            scene bg int_wine_basement with Dissolve(0.5)       #под сценой
            "...оказался в винном погребе"
            bgd "ого, {w}сколько вина!"
            "пока Богдан культурно удивлялся, сзади него послышались звуки ахуя"
            mz "НИХУЯЯЯЯЯЯ, {w}СКОЛЬКО ВИНАААААААААА"
            show mz smile glasses pioneer with Dissolve(0.5)    #Женя улыбается
            "Женя пускала слюни и уже явно забыла, для чего они суда с Богданом пришли"
            show mz smile glasses pioneer far at left           #Женя дальше
            mz "УХХХ НИХУЯ, {w}ГРУЗИНСКОЕ... {w}1945 ГОДА, ЕБАТЬ, БОГДАН, НЕ ЗРЯ МЫ СУДА ПРИШЛИ"
            bgd "эммм... {w}Жень, {w}мы суда за усилителями пришли..."
            "Богдан, отошедший от шока, стал шарить глазами в поиске искомых усилителей"
            "они компактно стояли в углу, рядом со шкафом с вином"
            bgd "Жень, бери вот этот, а тот я возьму"
            "Богдан указал Жене на усилитель, а сам взял второй и потащил к выходу"
            show mz normal glasses pioneer at center            #Женя нормальная
            "Женя наконец отошла от эйфории и последовала примеру Богдана"
            "в это время Богдан дошел до двери и толкнул ее наружу"
            
            play sound sfx_campus_door_rattle                   #попытка открыть дверь
            $ renpy.pause(0.5)
            play music music_list["no_tresspassing"]            #тревожная музыка
            th "хмм, заклинило, что ли?"
            bgd "Жень, нас заперли"
            mz "ну, попробуй еще открыть"
            play sound sfx_campus_door_rattle                   #попытка открыть дверь
            $ renpy.pause(0.5)
            bgd "неееее, что то не открывается"
            show mz smile glasses pioneer                       #Женя улыбается
            mz "какая жалость"
            stop music fadeout 1.0
            "наигранно произнеся слова, Женя подошла к шкафу и взяла бутылку"
            show mz smile glasses pioneer far at left           #Женя дальше
            play music khdMusicList["pofig"]                    #музыка пофиг
            mz "Богдан ты будешь?"
            th "охххх, бля, {w}ну я попал"
            bgd "нет, спасибо"
            mz "ну и здорово, мне больше достанется"
            play sound khdAmbList["drinking"]                   #звук питья
            "с этими словами Женя разбила горлышко бутылки о шкаф и начала эстетично дегустировать 
            вино, закрыв глаза и слегка подняв брови, {w}осознав, что вино добротное, Женя начала 
            пить его взахлеб"
            th "а она не свалится от этого?"
            stop sound
            "как бы отвечая на вопрос Богдана, допив бутылку, Женя пошатнулась, но тут же потянулась 
            за второй"
            bgd "может не надо?"
            mz "ч-ч-что?"
            "поняв, что перед ним уже не примерная интеллигентная пионерка, а сильно пьяная примерная 
            интеллигентная пионерка, Богдан решил действовать"
            show mz smile glasses pioneer close                 #Женя близко
            "Богдан подошел к Жене, уже замахнувшейся бутылкой, дабы открыть ее наполеоновским 
            способом, забрал бутылку, которая буквально вывалилась у нее из рук, стоило Богдану лишь 
            прикоснуться к ней, взял ее за руку и потянул в сторону от шкафов"
            "но Богдан не учел то, что ноги у Жени были ватные, {w}ну и короче, все как в обычных 
            модах"
            
            window hide
            show blink
            play sound sfx_fall_wood_floor                      #падение
            $ renpy.pause(0.5)
            scene black
            $ renpy.pause(0.5)
            scene bg int_wine_basement                          #в погребе
            show mz shy glasses pioneer close                   #Женя смущена
            show unblink
            window show

            "когда Богдан открыл глаза, он понял, что они вместе с Женей упали, аккурат друг на друга"
            th "хммм, {w}она вроде пьяная, сопротивляться не будет, {w}может выебать ее?"
            mz "о дааа, Богдан... {w}иик...{w} возьми меня прямо здесь"
            th "чето она слишком пьяная"
            "Богдан немножко оттащил Женю, сам сел у стенки спиной и положил ее головой себе на 
            живот"
            show mz smile glasses pioneer close                 #Женя улыбается                 
            th "ну а хуле, {w}не на голом холодном полу ведь ей лежать?"
            "посидев немного в тишине, Богдан все же нарушил молчание"
            stop music fadeout 1.0
            bgd "эй, Жень, {w}как мы отсюда выбираться будем?"
            mz "ой, да Дора придет и нам откроет, вот и выйдем"
            bgd "а что мы ей скажем по поводу того, что ты бухая в хлам?"
            mz "ой да ладно тебе, {w}не настолько и в хлам"
            "с этими словами Женя забралась чуть повыше на Богдана, чтобы лечь поудобнее"

            play music music_list["raindrops"]                  #романтичная музыка
            mz "ты лучше расскажи, кто ты, откуда и т д, {w}мы ж с тобой почти ничего не знаем друг 
            про друга"
            th "о, ну может удастся у нее выведать про то, где находится Совенок и какой сейчас год?"
            bgd "ну, {w}в общем слушай, {w} меня зовут Д****** Богдан Семенович, {w}мне 16 лет, {w}
            я родился и вырос в СССР, в городе Жуковск"
            mz "стой, Богдан, {w}а Жуковск... {w}это где?"
            bgd "в Удмуртии"
            mz "и че, ты из Удмуртии сюда приехал?"
            bgd "а не мог, что ли?"
            mz "ну мог, конечно"
            th "что-то тут не так..."
            bgd "ну, в общем, далее... {w}мои родители: папа - инженер конструктор, мама - бухгалтер"
            mz "кто-кто?"
            bgd "ну, то есть, кассир она"
            mz "а, понятно"
            bgd "сейчас я в 10 класс перехожу, а там... {w}не знаю, {w}в пту наверно пойду"
            mz "а чем увлекаешься, ну, кроме чтения?"
            bgd "на гитарах играю"
            mz "аче в музклуб не вступил?"
            bgd "да мне тамошний контингент не нравится... {w}Мику болтает много, {w}а Алиса уж вспыльчива очень"
            mz "дааа, есть такое... {w}в общем слушай про меня, {w}я - Евгения Онегина"
            "Богдан прыснул, услышав инициалы"
            mz "эээй, ты чего смеешься?"
            bgd "да просто имя у тебя книжное, {w}Евгения Онегина, как у Пушкина прямо"
            mz "Пушкин это кто?"
            th "че нахрен?"
            bgd "ну как, {w}Пушкин - наше все, величайший поэт 19 века"
            mz "слушай, Богдан, тут кто из нас бухой?"
            th "видимо в этой вселенной Пушкина нет"
            bgd "да ладно, пошутил я"
            mz "тогда я продолжу... {w}родилась здесь, на Сахалине"
            th "ебать куда меня занесло"
            mz "ну и сейчас мне 15 годиков, перехожу в 9, после 10 в универ думаю, {w}не знай на кого"
            mz "из увлечений - чтение, рисование, ну, и как видишь - винопитие, хехе"
            bgd "а что рисуешь?"
            mz "да... психодел всякий, {w}училась в художке, в итоге закончила ее, ну и психодел начала рисовать"
            stop music fadeout 1.0

            play sound sfx_unlock_door_campus                   #звук открытия дверей
            "в это время послышалось, как кто-то открывает двери"
            play music music_list["revenga"]                    #энергичная музыка
            th "ну все, {w}пизда нам"
            sl "эййй, Женя, Богдан, {w}вы здесь?"
            th "ну, хорошо, хоть Славя"
            bgd "дада, сейчас"
            show mz smile glasses pioneer at right              #Женя улыбается
            "Богдан встал, поднял на ноги Женю, она уже более ли менее твердо стояла"
            
            show bg ext_stage_normal_day                        #сцена
            show sl shy pioneer at left                         #Славя смущена
            with Dissolve(0.5)
            "свет резко ударил в глаза Богдану, поначалу он прищурился"
            sl "вы меня простите, я думала там нет никого, вот и закрыла, а потом мне Мику сказала, что там вы 
            находитесь"
            mz "о, привет, Славя, да не парься ты так, все норм"
            show sl surprise pioneer at left                    #Славя удивлена
            sl "Жень, а что ты такая довольная... {w}и почему от тебя вином несет?"
            show sl angry pioneer at left                       #Славя злая
            sl "вы что там, выпили?"
            bgd "не мы, а Женя, {w}у меня ни в одном глазу"
            "поспешил оправдаться Богдан, {w}Женю же ничего не смущало"
            show mz shy glasses pioneer at right                #Женя смущается
            mz "ну даа, {w}иик, {w}и что тут такого?"
            sl "КАК ЧТО, ТЫ ДУРА КОНЧЕНАЯ, СЕГОДНЯ СУББОТНИК, А ЗНАЧИТ НУЖНО ТРУДИТЬСЯ, А НЕ ПИТЬ В ПОДВАЛЕ..."
            "пока Славя остановилась, дабы набрать воздух, Богдан решил поговорить о насущном"
            bgd "Славя, а если ее увидят? {w}Надо бы ее протрезвить по-быстрому"
            show sl scared pioneer at left                      #Славя напугана
            "Славя мигом изменилась в лице и судрожно пыталась что-то придумать"
            sl "... блин, {w}давай ее к нам в домик, {w}а там... {w}о, {w}у меня ж горчица есть, {w}мигом 
            протрезвим"
            show sl serious pioneer at left                     #Славя серьезная
            bgd "ладно, давай"
            hide mz shy glasses pioneer with Dissolve(0.5)      #Женя прячется    
            "с этими словами Богдан взвалил Женю себе на спину и вместе со Славей пошел до их дома"
            mz "ойй, Богдан, {w}а почему меня укачивает?"
            show bg ext_house_of_sl_day with Dissolve(0.5)      #домик Жени и Слави
            bgd "потому, что ты на мне едешь"
            mz "аааа, понятно"
            th "даааа, одна бутылка на нее сильно подействовала"
            "благо, дом Жени и Слави был близко к сцене"

            show bg int_house_of_sl_day with Dissolve(0.5)      #в домике
            play sound sfx_open_door_1                          #дверь открывается
            stop music fadeout 1.0
            play ambience ambience_int_cabin_day loop           #эмбиенс в домике
            "в домике напряжение спало"
            show sl normal pioneer                              #Славя нормальная
            sl "ладно, клади ее суда"
            "Славя указала на одну из кроватей, {w}Богдан подошел и бережно посадил Женю, {w}она еле 
            сидела, опираясь руками на край кровати и смотря вниз"
            show sl normal pioneer at goRight                   #Славя отходит
            play sound khdAmbList["metal_noise"]                #звук поиска
            sl "сейчас, сейчас... {w}о, {w}нашла!"
            stop sound
            show sl normal pioneer at center with Dissolve(0.5) #Славя нормальная
            "Славя достала баночку с желтой горчицей, зачерпнула ее столовой ложкой и засунула в 
            рот Жене"

            window hide
            play sound sfx_angry_ulyana                         #закипание чайника
            $ renpy.pause(3.0)
            window show

            show mz angry glasses pioneer at left               #Женя злая
            mz "АААААА, ВОООДЫЫЫЫ"
            "Женя резко протрезвела и пулей выскочила из домика в поисках жидкости"
            hide mz angry glasses pioneer with Dissolve(0.5)    #Женя уходит
            show sl laugh pioneer                               #Славя смеется
            "Славя с Богданом не выдержали и засмеялись"
            bgd "ну и чудо элексир, как чифир прямо"
            play sound sfx_dinner_horn_processed                #звук на обед
            "пока Богдан и Славя откачивали Женю, уже настал обед"
            bgd "ну ладно, пойдем"
            show sl normal pioneer                              #Славя нормальная
            sl "ну да, пойдем, {w}думаю, Женя нас догонит"
            
            play sound sfx_close_door_1                         #дверь закрывается
            show bg ext_house_of_sl_day with Dissolve(0.5)      #домик Жени и Слави
            play ambience ambience_camp_center_day              #эмбиенс в лагере
            "поначалу Богдан со Славей шли молча, но Богдан решил нарушить тишину"
            bgd "эй, Славь, {w}ты то как субботник провела?"
            sl "ну, площадь подметала, {w}потом на перерыве пошла к сцене цветы проверить, вижу - 
            дверь открыта, дай думаю закрою, {w}а потом пошла вас вызволять оттуда, когда поняла, 
            что сделала"
            show bg ext_square_day with Dissolve(0.5)           #площадь
            sl "а кстати, вы зачем туда ходили то?"
            "и тут Богдан понял, что усилители то они не вынесли"
            bgd "ай, тьфу ты, нас Мику попросила усилители вынести, а я там с Женей и забыл про это"
            bgd "ладно, после обеда вытащу"
            sl "о, Женя"
            
            show sl normal pioneer at left                      #Славя нормальная
            show mz normal glasses pioneer at right with Dissolve(0.5) #Женя нормальная
            bgd "ну че, как?"
            show bg ext_dining_hall_away_day with Dissolve(0.5) #столовая далеко
            mz "голова болит..."
            show sl surprise pioneer at left                    #Славя удивлена
            sl "похмелье что ли?"
            mz "ну да, что-то типо того"
            show bg ext_dining_hall_near_day with Dissolve(0.5) #рядом со столовой
            "к этому времени наша троица подошла к столовой"
            hide sl surprise pioneer
            hide mz normal glasses pioneer                      #Славя и Женя прячутся
            with Dissolve(0.5)              
        "неее, мне еще в библиотеке убираться":
            th "нууу блин, еще и Мику помогать? {w}не, не хочу"
            bgd "знаешь, Мику, мы тут еще не закончили, у нас еще работы по горло..."
            show mi sad pioneer at cright                       #Мику грустная
            mi "ну блин, придется просить помощи у кибернетиков"
            hide mi sad pioneer with Dissolve(0.5)              #Мику уходит
            play sound sfx_close_door_1                         #закрывается дверь
            stop music fadeout 1.0

            show mz bukal glasses pioneer at center             #Женя с презрением
            mz "вот ведь а? {w}темноты она боится, видите ли"
            hide mz bukal glasses pioneer with Dissolve(0.5)    #Женя уходит
            "поворчав Женя пошла обратно делать уборку, {w}Богдан тоже"
            "..."
            bgd "ну все, интересные книги кончились"
            "через часик Женя с Богданом встретились у одного из шкафов, а значит закончили все 
            протирать"
            show mz normal glasses pioneer with Dissolve(0.5)   #Женя нормальная
            mz "ну, теперь надо внести новые книжки в базу"
            "Женя указала на большую стопку книг, лежавших у входа"
            bgd "а какая у тебя база? какая субд? mysql&?"
            show mz smile glasses pioneer                       #Женя улыбается
            mz "что-что?"
            bgd "как вносить то их будем, где кампутер?"
            show mz bukal glasses pioneer                       #Женя с презрением
            mz "какой кампутер тебе, ручкой по бумажке писать будем, кампутер он захотел, мы в 
            Америке что ли по-твоему?"
            bgd "ну у кибернетиков ведь есть кампутер"
            mz "пффф, там жесткий диск на 16 мегабутов, а тут у меня все все все: {w}от некрологов до
            детских книжек"
            th "она шарит что ли?"
            bgd "а ты шаришь за мегабуты?"
            mz "неа, мне так кибернетики сказали"
            bgd "ну ладно"

            show cg mz_lib with Dissolve(0.5)                   #Женя за столом
            "Женя зачем-то сняла очки, когда они с Богданом уселись друг напротив друга за стол"
            th "блин, а без очков ей больше идет"
            bgd "а зачем ты очки сняла?"
            mz "ну, когда я смотрю на что-то близко без очков удобней как-то"
            bgd "пон"
            mz "ну, короче смотри, вот тебе листочек, в первую колонку записываешь порядковый номер 
            книги, во вторую название, в третью номер шкафа и полки, у тебя 3 шкаф 27 полка, {w}все 
            понятно?"
            bgd "ага"
            mz "да, и еще, {w}потом все свои книги складывай в отдельную стопку"
            bgd "ладно"
            play music music_list["reflection_on_water"]        #музыка
            "Богдан приступил к работе, {w}среди множества скучных книг, то и дело проскакивала 
            какая-нибудь научная фантастика Стругацких или еще кого"
            "Богдан записывал черной ручкой все как сказала Женя: {w}порядковый номер, название, 
            3.27"
            "Богдану не нравилось молчание, {w}оно ему казалось каким-то неловким"
            th "про что же поговорить с ней?"
            "глобальные мысли тужились в мозгу..."
            bgd "Жень, {w}а ты пойдешь сегодня на дискотеку?"
            mz "не знаю, как получится"
            th "бля, еще хуже стало"
            bgd "а что вдруг так? {w}свои планы на вечер?"
            mz "слушай, не отвлекай, {w}на книгах лучше сконцентрируйся"
            th "обоссан и слит"
            "..."
            "остаток субботника Богдан проводил молча"
            "25 | Мертвые души | 3.27"
            "26 | Улитка на склоне | 3.27"
            "27 | Капитал | 3.27"
            "..."

            scene bg int_library_day                            #библиотека
            show mz normal pioneer                              #Женя нормальная 
            with Dissolve(0.5)
            bgd "все, я закончил"
            show mz bukal pioneer                               #Женя с презрением
            mz "а, ну класс, {w}тогда отнеси их на свою полку, они пронумерованы"
            hide mz bukal pioneer with Dissolve(0.5)            #Женя прячется
            "Богдан встал и унес свои книги на 27 полку 3 шкафа"
            "Женя тоже успела закончить описывать свои книги"
            show mz normal pioneer with Dissolve(0.5)           #Женя нормальная
            "после чего они вместе подошли с Богданом к столу, {w}Женя надела очки"
            show mz normal glasses pioneer                      #Женя в очках
            mz "до обеда еще полчаса где-то, {w}чай может попьем?"
            bgd "давай"
            th "а как она собралась здесь чай заваривать"
            "тем временем Женя достала из шкафа водонагреватель, похожий на люминисцентную лампу, и 
            опустила его в банку с водой, стоящую на подоконнике"
            th "а это не для цветов случайно?"
            mz "ты чай с сахаром или без пьешь?"
            bgd "без, конечно"
            "вскоре вода в банке закипела, и Женя выключила нагреватель, после чего она взяла 
            полотенце и, как прихваткой, взяла им банку и разлила кипяток по чашкам, {w}после чего 
            она добавила в чашки заварку черного чая и помешала ложкой"
            "одну из чашек она протянула Богдану"
            show mz smile glasses pioneer                       #Женя улыбается
            mz "приятного аппетита"
            bgd "спасибо, тебе тоже"
            bgd "это что за чай такой?"
            mz "индийский"
            bgd "а откуда?"
            show mz normal glasses pioneer                      #Женя нормальная
            mz "не знаю, тут в тумбочке был"
            th "вот это поворот"
            bgd "ну, вкусно, вкусно, спасибо"
            "опять повисло неловкое молчание"
            "вскоре чашка с чаем кончилась, что озночало логическое завершение нахождения Богдана 
            в библиотеке"
            play sound sfx_dinner_horn_processed                #горн на обед
            $ renpy.pause(1.0)
            "тем более, прозвучал сигнал к обеду"
            mz "ой, блин, {w}засиделись мы здесь... {w}ладно, ты иди, я тут пока все уберу"
            th "мммм, ну ладно"
            bgd "ну тогда давай, {w}может увидимся"
            mz "пока"
            
            scene bg ext_library_day with Dissolve(0.5)         #снаружи библиотека
            play sound sfx_open_door_1                          #дверь открывается
            play ambience ambience_camp_center_day loop         #эмбиенс в лагере
            stop music fadeout 1.0
            "выйдя из библиотеки, Богдан пошел на обед один"
            show bg ext_square_day with Dissolve(0.5)           #площадь
            th "как весело одному то ходить"
            show bg ext_dining_hall_away_day with Dissolve(0.5) #столовая дальше
            $ renpy.pause(0.5)
            show bg ext_dining_hall_near_day with Dissolve(0.5) #столовая рядом
            "Богдан подошел к столовой и зашел внутрь"

    return
        
#***************************** ПРОДОЛЖЕНИЕ УТРА С КХД (ЕСЛИ СУББОТНИК С КХД) **********
label titanMorningDay2khd:
    show ant norm at cleft                                      #Антон
    show mkr norm at cright                                     #Икар
    with Dissolve(0.5)
    "так как субботник Богдану надо было делать с кибернетиками, он нашел их и встал вместе с ними"
    bgd "йоу, всем привет"
    mkr "привет, привет"
    ant "доброе утро"
    "Богдан с кибернетиками перешептывался и ковырялся в носу, пока Дора что-то рассказывала про сегодняшний день 
    и говорила всем, как хорош труд!"
    th "ну бля, Дор, заканчивай уже, {w}где сигнал к завтраку?"
    play sfx_dinner_horn_processed                              #горн на завтрак

    th "о, а вот и он"
    dor "ну, все, ребята, можем идти на завтрак"
    bgd "да ну, наконец-то"
    play music music_list["went_fishing_caught_a_girl"]         #веселая музыка
    "Богдан пошел вместе с кибернетиками на завтрак, предвкушая предстоящий субботник в весьма веселой гачи 
    обстановке"
    show bg ext_dining_hall_away_day with Dissolve(0.5)         #столовая далеко
    bgd "Икар, а нам много там убираться?"
    mkr "да не особо, мы вчера с Тохой подсчитали, там в кладовке убраться, новые радиодетали рассортировать, ну и
    пол помыть"
    th "ну, звучит не сильно сложно, {w}тем более нас трое"
    